program		= expression ;
expression	= block
			| assignment ;

(* Expressions *)
assignment	= index {'=' expression}
			| function ;
function	= (Identifier | group) '=>' expression
			| comparison ;
comparison	= bitwise {('==' | '!=' | '<' | '>' | '<=' | '>=') bitwise} ;
bitwise		= addsub {('>>' | '<<') addsub} ;
addsub		= muldiv {('+' | '-') muldiv} ;
muldiv		= unary {('*' | '/') unary} ;
unary		= ('-' | '!') unary
			| call ;
call		= index index {index} ;
index		= primary {'[' expression ']'} ;
primary		= Identifier | Number | String | group ;
group		= '(' explist ')' ;

(* Blocks and statements *)
block		= '{' {statement} '}';
statement	= 'return' expression
			| 'yield' expression
			| 'if' expression ':' statement ['else' statement]
			| 'while' expression ':' statement
			| expression ;

(* Smaller components *)
arglist		= Identifier {',' Identifier} ;
explist		= expression {',' expression} ;
