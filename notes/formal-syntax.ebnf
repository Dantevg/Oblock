program		= expression ;
expression	= control ;

(* Expressions *)
control		= 'if' expression ':' statement ['else' statement]
			| 'while' expression ':' statement
			| 'for' patlist 'in' expression ':' statement
			| function ;
function	= fn | disjunction ;
disjunction	= conjunction {'or' conjunction} ;
conjunction	= application {'and' application} ;
application	= comparison {'|>' comparison} ;
comparison	= bitwise {('==' | '!=' | '<' | '>' | '<=' | '>=') bitwise} ;
bitwise		= addsub {('>>' | '<<') addsub} ;
addsub		= muldiv {('+' | '-') muldiv} ;
muldiv		= unary {('*' | '/') unary} ;
unary		= ('-' | '!' | '...') unary | varcall ;
varcall		= [primary] {'.' primary | primary} ;
primary		= Identifier | Number | String | group | list | block ;
group		= '(' expseq ')' ;
list		= '[' expseq ']' ;
block		= '{' statseq '}' ;

(* Statements *)
statement	= 'return' [explist]
			| 'yield' [explist]
			| 'break' {'break'} [explist]
			| 'continue'
			| ';'
			| definition ;
definition	= {modifier} (Identifier {',' Identifier}) ['=' explist | fn]
			| assignment ;
assignment	= patlist ['=' explist | fn]
			| expression ;

(* Patterns *)
pattern		= Identifier | Number | String | grouppat | listpat | blockpat ;
grouppat	= '(' patexpseq [restpat] patexpseq ')' ;
listpat		= '[' patexpseq [restpat] patexpseq ']' ;
blockpat	= '{' patstatseq [restpat] patstatseq '}' ;
(* varpat		= '\(' Identifier ')' ; *)
restpat		= '...' Identifier ;
patexpseq	= {pattern | ','} ;
patstatseq	= {Identifier '=' expression | Identifier | ';'} ;

(* Smaller components *)
fn			= pattern '=>' expression ;
expseq		= {primary '=' expression | expression | ','} ;
statseq		= {statement} ;
explist		= expression {',' expression} ;
patlist		= pattern {',' pattern} ;
modifier	= 'var' | 'const' ;
