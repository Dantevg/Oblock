program		= expression ;
expression	= function ;

(* Expressions *)
function	= fn | disjunction ;
disjunction = conjunction {'||' conjunction} ;
conjunction	= comparison {'&&' comparison} ;
comparison	= bitwise {('==' | '!=' | '<' | '>' | '<=' | '>=') bitwise} ;
bitwise		= addsub {('>>' | '<<') addsub} ;
addsub		= muldiv {('+' | '-') muldiv} ;
muldiv		= unary {('*' | '/') unary} ;
unary		= ('-' | '!' | '...') unary
			| call ;
call		= index index {index} ;
index		= [primary] {'.' (Identifier | '[' expression ']')} ;
primary		= Identifier | Number | String | group | list | block ;
group		= '(' explist ')' ;
list		= '[' explist ']' ;
block		= '{' statlist '}' ;

(* Statements *)
statement	= 'return' expression
			| 'yield' expression
			| 'if' expression ':' statement ['else' statement]
			| 'while' expression ':' statement
			| 'for' Identifier 'in' expression ':' statement
			| definition ;
definition	= ('var' | modifier {modifier}) index ['=' function | fn]
			| index ':=' function
			| assignment ;
assignment	= index '=' function
			| expression ;

(* Smaller components *)
fn			= (Identifier | '(' paramlist ')') '=>' expression ;
explist		= {expression | definition | ','} ;
statlist	= {statement | ';'} ;
paramlist	= param {',' param} ;
param		= ['...'] Identifier ;
modifier	= 'const' | 'instance' ;
