program		= expression ;
expression	= block
			| assignment ;

(* Expressions *)
assignment	= variable {'=' assignment}
			| function ;
function	= (variable | group) '=>' expression
			| comparison ;
comparison	= bitwise {('==' | '!=' | '<' | '>' | '<=' | '>=') bitwise} ;
bitwise		= addsub {('>>' | '<<') addsub} ;
addsub		= muldiv {('+' | '-') muldiv} ;
muldiv		= unary {('*' | '/') unary} ;
unary		= ('-' | '!') unary
			| call ;
call		= primary primary {primary} ;
primary		= variable | Number | String | group ;
group		= '(' explist ')' ;

(* Blocks and statements *)
block		= '{' {statement} '}';
statement	= 'return' expression
			| 'if' expression ':' statement ['else' statement]
			| 'while' expression ':' statement
			| expression ;

(* Smaller components *)
variable	= Identifier ;
arglist		= Identifier {',' Identifier} ;
explist		= expression {',' expression} ;
